apply from: "$rootDir/gradle/application.gradle"
apply from: "$rootDir/gradle/sdkman.gradle"

if( JavaVersion.current().java7Compatible ) {
    apply from: "$rootDir/gradle/publishing.gradle"
}

description = "The binary distribution package that bundles JBake cli"

sourceSets {
    smokeTest {
        java {
            srcDir file('src/smoke-test/java')
        }
        resources {
            srcDir file('src/smoke-test/resources')
        }
    }
}

configurations {
    dist

    runtime.extendsFrom dist
}


dependencies {
    compile project(':jbake-core')
    compile("org.asciidoctor:asciidoctorj-diagram:$asciidoctorjDiagramVersion") {
        exclude group: 'org.asciidoctor', module: 'asciidoctorj'
    }

    // TODO: remove as soon as java 7 support drops
    if ( JavaVersion.current().isJava7() ) {
        smokeTestCompile'org.eclipse.jgit:org.eclipse.jgit:4.5.3.201708160445-r'
    } else {
        smokeTestCompile'org.eclipse.jgit:org.eclipse.jgit:5.1.2.201810061102-r'
    }
    smokeTestCompile configurations.testCompile
    smokeTestCompile sourceSets.test.output
    smokeTestRuntime configurations.testRuntime
}

artifacts {
    archives distZip
}

task smokeTest(type: Test, dependsOn: installDist) {
    group 'Verification'
    description 'Runs the integration tests.'
    setTestClassesDirs sourceSets.smokeTest.output.classesDirs
    classpath = sourceSets.smokeTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn smokeTest
