import java.text.SimpleDateFormat

buildscript {
    dependencies {
        classpath group: "com.liferay", name: "com.liferay.gradle.plugins.maven.plugin.builder", version: "1.2.0"
    }

    repositories {
        maven {
            url "https://repository-cdn.liferay.com/nexus/content/groups/public"
        }
    }
}

apply plugin: 'java'
apply plugin: "com.liferay.maven.plugin.builder"
apply plugin: 'maven'

description = "Maven Plugin for JBake"
dependencies {
    compile project (":jbake-core")
    compile "org.eclipse.jetty:jetty-util:$jettyServerVersion"
    compile "org.eclipse.jetty:jetty-server:$jettyServerVersion", optional
    compile "org.apache.maven:maven-plugin-api:3.0"
    compile "org.apache.maven:maven-settings:3.0"
    compile "org.apache.maven:maven-core:3.0"
    compile "org.sonatype.plexus:plexus-build-api:0.0.7"
    compile "org.codehaus.plexus:plexus-utils:3.0.22"
    compileOnly "org.apache.maven.plugin-tools:maven-plugin-annotations:3.4"
}

Date buildTimeAndDate = new Date()

ext {
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

def jbakePom = pom {
    project {
        groupId = project.group
        artifactId = "jbake-maven-plugin"
        version = project.version
        name "jbake-maven-plugin"
        description project.description
        url project.website
        developers {
            developer {
                id 'jonbullock'
                name 'Jonathan Bullock'
                email 'jonbullock@gmail.com'
                url 'http://jonathanbullock.com'
                timezone 'Europe/London'
            }
            developer {
                id 'ancho'
                name 'Frank Becker'
                email 'frank@calmdevelopment.de'
                timezone 'Europe/Berlin'
            }
            developer {
                id 'manikmagar'
                name 'Manik Magar'
                url 'https://manik.magar.me/'
                timezone 'America/New_York'
            }
        }
        scm {
            url project.vcs
            connection 'scm:git:git@github.com:jbake-org/jbake.git'
            developerConnection 'scm:git:https://github.com/jbake-org/jbake.git'
        }
        issueManagement {
            system 'GitHub Issues'
            url project.issues
        }
        mailingLists {

            mailingList {
                name 'jbake-dev'
                subscribe 'jbake-dev@googlegroups.com'
                unsubscribe 'jbake-dev+unsubscribe@googlegroups.com'
                archive 'http://groups.google.com/group/jbake-dev'
            }
            mailingList {
                name 'jbake-user'
                subscribe 'jbake-user@googlegroups.com'
                unsubscribe 'jbake-user+unsubscribe@googlegroups.com'
                archive 'http://groups.google.com/group/jbake-user'
            }
        }

        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'http://opensource.org/licenses/MIT'
            }
        }
    }
}

jar {
    manifest {
        attributes(
                'Built-By': System.properties['user.name'],
                'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
                'Build-Date': buildDate,
                'Build-Time': buildTime,
                'Specification-Title': project.name,
                'Specification-Version': project.version,
                'Specification-Vendor': project.name,
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': project.name
        )
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

install {
    repositories.mavenInstaller {
        setPom jbakePom
    }
}

writeMavenSettings {
    outputFile = 'src/main/resources/META-INF/maven/settings.xml'
}
buildPluginDescriptor {
    goalPrefix = "jbake"
    pomGroupId = 'org.jbake'
    pomArtifactId = 'jbake-maven-plugin'
    pomVersion = project.version
    classesDir = 'build/classes/java/main'
    mavenPluginPluginVersion = '3.5.2'
//    mavenDebug = true
}